pkgname=pex-loader-git
pkgdesc='A library for loading and parsing PEX files. Written for pyke'
pkgver=1.0.0_1_g0422659
pkgrel=1
license=('Apache')
arch=(x86_64)
url='https://github.com/kodo-pp/pex-loader'

depends=(libbinary-format)
provides=(pex-loader)
makedepends=(meson ninja)

source=('git+https://github.com/kodo-pp/pex-loader')
sha256sums=('SKIP')

pkgver() {
    cd "${srcdir}/pex-loader"
    git describe --tags | tr - _
}

build() {
    cd "${srcdir}/pex-loader"
    meson setup build -Dbuildtype=release --prefix=/usr
    cd build
    ninja
}

check() {
    cd "${srcdir}/pex-loader/build"
    ./catch2_test
}

package() {
    cd "${srcdir}/pex-loader/build"
    DESTDIR="${pkgdir}" ninja install
}
